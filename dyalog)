#!/usr/local/bin/dyalogscript
Ï ← 2 ⎕FIX 'file://',⊢

Ï 'Safe.dyalog'
Ï 'cmd.apl'
Ï 'explain.aplf'
Ï 'help.aplf'
'display'⎕cy'dfns'

Ⓞ ← {⍺←⊢ ⋄ ((⍺⍺ ⍺)⍵⍵⊢)⍵}

∇ r ← Exec expr;ns

    ns ← ⎕NS ⍬
    ns.display ← display
    ns.explain ← Explain
    ns.help    ← Help
    
    :Trap 0
        r←5 ns Safe.Exec expr
    :Case 6
        r←'Shy'
    :Case 10
        r←'EXPRESSION TIME LIMIT EXCEEDED: Must complete within 5 seconds'
    :Case 11
        r←'Illegal Token'
    :Else
        r←⎕DMX.(Message{⍵,⍺,⍨': '/⍨×≢⍺}⎕EM 200|EN-200)
    :EndTrap
    
    :If 0=×/⍴r ⋄ r ← 'Empty' ⋄ :EndIf
∇

Shrink ← { 
    w ← ⊃,/⍵
    n ← ≢w
    l ← ≢¨⍵
    s ← 2000((3-⍨⊣)-+.>)+\1,l
    s>n : ⍵
    '```' ,∘⊂⍨ w⊆⍨n↑s↑⍸l
}

⎕ ← 1 ⎕JSON Shrink ⊃,/{((⊂⍤¯1) ⎕FMT Exec ⍵),⍥('```'⊂Ⓞ,⊢)⍬}¨ ⍎ 2 ⎕NQ # 'GetEnvironment',⊂'expr'