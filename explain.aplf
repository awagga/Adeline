∇ expr ← {valence} Explain train
 :With ⎕NS ⍬
   valence←{0::1⋄valence}⍬
    F ← ⊃,/
    train ← F ''''''∘,¨@(''''∘=)train
    f       ← '+-×÷⌈⌊*⍟|!○~∨∧⍱⍲<≤=≥>≠≡≢⍴,⍪⍳↑↓?⍒⍋⍉⌽⊖∊⊥⊤⍎⍕⌹⊂⊃∪∩⍷⌷⊆⊣⊢⍸'
    m∆op    ← '/⌿\⍀¨&⍨⌶⌸'
    d∆op    ← '.@∘⍛⍣⍥⍠⍤⌺'
    slashes ← '/⌿\⍀'

    Pad  ← '('∘,,∘')'
    Name ← ' ',⍨,∘⍕∘⎕UCS

    (f∆n m∆n d∆n) ← ' primitive∆'∘Name¨¨f m∆op d∆op
    (s∆n)         ← ' slash∆'∘Name¨slashes

    _Lit←⍠'Regex' 0
    train ← ((,d∆op∘.,slashes)⎕R(,d∆op∘.,s∆n,¨' ')_Lit)train ⍝ @/

    _Assign_ ← {⍎F ⍺ ⍺⍺(⍕⍵)⍵⍵}
    (f∆n,s∆n)('←{⍺←⊢ ⋄ r ← (⍕⍺),'''_Assign_''',(⍕⍵) ⋄ ⍬≢⍵: Pad r ⋄ r}')¨f,slashes

    (m∆n)('←{⍺←⊢ ⋄ F Pad ⍺(⍺⍺ ⍬)'''_Assign_'''        ⍵}')¨m∆op
    (d∆n)('←{⍺←⊢ ⋄ F Pad ⍺(⍺⍺,⍬)'''_Assign_''' (⍵⍵,⍬) ⍵}')¨d∆op

    P9064 ← {⍺←⊢ ⋄ ⍵≡⍬: F (⍺⍺ ⍬)'⍨'       ⋄ ⍺ (⍺⍺⍨)   ⍵}
    P9061 ← {⍺←⊢ ⋄ ⍵≡⍬: F (⍺⍺ ⍬)'⍥'(⍵⍵ ⍬) ⋄ ⍺ ⍺⍺⍥⍵⍵   ⍵}
    P8728 ← {⍺←⊢ ⋄ ⍵≡⍬: F (⍺⍺ ⍬)'∘'(⍵⍵ ⍬) ⋄ ⍺ ⍺⍺∘⍵⍵   ⍵}
    P9060 ← {⍺←⊢ ⋄ ⍵≡⍬: F (⍺⍺ ⍬)'⍤'(⍵⍵ ⍬) ⋄ ⍺ ⍺⍺⍤⍵⍵   ⍵}
    P9051 ← {⍺←⊢ ⋄ ⍵≡⍬: F (⍺⍺ ⍬)'⍛'(⍵⍵ ⍬) ⋄ ⍺ ⍵⍵⍨∘⍺⍺⍨ ⍵}

    Fun ← ⍎((,¨f,m∆op,d∆op)⎕R(f∆n,m∆n,d∆n)_Lit)train

    expr ← (Fun '⍵')(⊣⍣(valence=1))('⍺'Fun'⍵')
    expr ← ('\((⍺)⊣⍵\)' '\(⍺?⊢(⍵)\)'⎕R'\1')expr

 :EndWith
∇